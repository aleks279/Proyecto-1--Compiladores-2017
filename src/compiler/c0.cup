package compiler;

import java.util.*;
import java_cup.runtime.*;
import codeGen.*;
import symTable.*;
import types.*;

action code {:
    public int addressCounter = 65535;
    public int whileCounter;
    public int loopCounter;
    public int ifCounter;
    public int printCounter;
    Stack<Integer> pile;
    Stack<Integer> loopPile;

    void initialize() {
        // Sym table stack initializer
        SymTable.initialize();
        
        // Initialize types
        Type.createTypes();
        CICode.listenCode();
        CICode.finalCodeList.clear();
        CICode.stringList.clear();
        //
        ifCounter=0;
        whileCounter=0;
        printCounter = 0;
        loopCounter = 0;
        pile = new Stack<Integer> ();
    }

    Expression plus(Expression e1, Expression e2){
        addressCounter--;
        CICode.saveCode("SUMAR",
            String.valueOf(e1.getAddress()),
            String.valueOf(e2.getAddress()),
            String.valueOf(addressCounter));
        return new Expression(addressCounter);
    }
    Expression minus(Expression e1, Expression e2){
        addressCounter--;
        CICode.saveCode("RESTAR",
            String.valueOf(e1.getAddress()),
            String.valueOf(e2.getAddress()),
            String.valueOf(addressCounter));
        return new Expression(addressCounter);
    }
    Expression mul(Expression e1, Expression e2){
        addressCounter--;
        CICode.saveCode("MULTIPLICAR",
            String.valueOf(e1.getAddress()),
            String.valueOf(e2.getAddress()),
            String.valueOf(addressCounter));
        return new Expression(addressCounter);
    }
    Expression div(Expression e1, Expression e2){
        addressCounter--;
        CICode.saveCode("DIVIDIR",
            String.valueOf(e1.getAddress()),
            String.valueOf(e2.getAddress()),
            String.valueOf(addressCounter));
        return new Expression(addressCounter);
    }
    Expression integer(String e){
        addressCounter--;
        CICode.saveCode("CARGAR_VALOR",
            e,
            "null",
            String.valueOf(addressCounter));
        return new Expression(addressCounter);
    }
    Expression id(String id){
        addressCounter--;
        CICode.saveCode("CARGAR_DIRECCION",
            String.valueOf((SymTable.get(id)).getAddress()),
            "null",
            String.valueOf(addressCounter));
        return new Expression(addressCounter);
    }


    Expression or(Expression c1, Expression c2){
        addressCounter--;
        CICode.saveCode("OR",
            String.valueOf(c1.getAddress()),
            String.valueOf(c2.getAddress()),
            String.valueOf(addressCounter));
        return new Expression(addressCounter);
    }
    Expression and(Expression c1, Expression c2){
        addressCounter--;
        CICode.saveCode("AND",
            String.valueOf(c1.getAddress()),
            String.valueOf(c2.getAddress()),
            String.valueOf(addressCounter));
        return new Expression(addressCounter);
    }
    Expression greater(Expression e1, Expression e2){
        addressCounter--;
       CICode.saveCode("MAYOR",
            String.valueOf(e1.getAddress()),
            String.valueOf(e2.getAddress()),
            String.valueOf(addressCounter));
        return new Expression(addressCounter);
    }
    Expression less(Expression e1, Expression e2){
        addressCounter--;
       CICode.saveCode("MENOR",
            String.valueOf(e1.getAddress()),
            String.valueOf(e2.getAddress()),
            String.valueOf(addressCounter));
        return new Expression(addressCounter);
    }
    Expression equal(Expression e1, Expression e2){
        addressCounter--;
       CICode.saveCode("IGUAL",
            String.valueOf(e1.getAddress()),
            String.valueOf(e2.getAddress()),
            String.valueOf(addressCounter));
        return new Expression(addressCounter);
    }
    Expression unequal(Expression e1, Expression e2){
        addressCounter--;
       CICode.saveCode("DISTINTO",
            String.valueOf(e1.getAddress()),
            String.valueOf(e2.getAddress()),
            String.valueOf(addressCounter));
        return new Expression(addressCounter);
    }

    //methods

    void assign(String s, Expression e){
        CICode.saveCode("CARGAR_DIRECCION",
            String.valueOf(e.getAddress()),
            "null",
            String.valueOf((SymTable.get(s)).getAddress()));
    }


    void condition(Expression e, int n){
        CICode.saveCode("SALTAR_CONDICION",
            String.valueOf(e.getAddress()),
            "null",
            "ELSE_"+String.valueOf(n));        
    }

    void condition_while(Expression e, int n) {
        CICode.saveCode("SALTAR_CONDICION",
            String.valueOf(e.getAddress()),
            "null",
            "FINBUCLE_"+String.valueOf(n));            
    }
    
    void jumpLabel(String label, int n){
        CICode.saveCode("SALTAR_CONDICION",
            "null",
            "null",
            label+"_"+String.valueOf(n));    
    }

    void putLabel(String label, int n){
        CICode.saveCode("ETIQUETA",
            "null",
            "null",
            label+"_"+String.valueOf(n));    
    }
    

    //print
    void printW(Expression e){
        CICode.saveCode(
            "IMPRIMIR_ENTERO",
            String.valueOf(e.getAddress()),
            "null",
            "null");
    }

    void printS(String s, int n) {
        CICode.stringList.add(s);
        CICode.saveCode(
            "IMPRIMIR_CADENA",
            "CADENA_"+String.valueOf(n),
            "null",
            "null");
    }

    void endProgram(){
        CICode.saveCode(
            "FIN",
            "null",
            "null",
            "null");
    }
:}

terminal PLUS, MINUS, MUL, DIV, ASSIGN;
terminal GREATER, LESS, EQUAL, UNEQUAL;
terminal AND, OR, MAIN, PUTW, PUTS, INTWORD;
terminal IF, ELSE, WHILE, BREAK;
terminal LPAREN, RPAREN, LBRACE, RBRACE, SEMICOLON;

terminal String ID;
terminal String INT;
terminal String STRING;

// non terminals
non terminal Program, Assigns, Body;
non terminal Declaration;
non terminal SentenceBlock;
non terminal Sentences, Sentence;
non terminal Expression Expression;
non terminal Expression Condition;
non terminal SentIf, SentElse;
non terminal SentWhile;
non terminal SentAssign;
non terminal SentPutw, SentPuts;
non terminal SentBreak;

precedence left ASSIGN;
precedence left OR, AND;
precedence left EQUAL, UNEQUAL, GREATER, LESS;
precedence left PLUS, MINUS;
precedence left MUL, DIV;
precedence left LPAREN, RPAREN;
precedence left ELSE;

start with Program;
// grammar

Program ::= {:
                initialize();
                
            :}
            Assigns Body | Body;

Assigns ::= Assigns Declaration | Declaration;

Declaration ::= INTWORD ID:id SEMICOLON {:
                if(!SymTable.existSymbol(String.valueOf(id))){
                    SymbolT sym = new SymbolT(id, types.Type.getType(types.Type.INTEGER), false, --addressCounter);
                    SymTable.insert(id, sym);
                } else {
                    int line = ((java_cup.runtime.Symbol)CUP$parser$stack.elementAt(CUP$parser$top)).left;
                    ERRORS.report(ERRORS.REPEATED_SYMBOL,line);
                }
            :};

Body ::= MAIN LPAREN RPAREN LBRACE SentenceBlock {:
                endProgram();
                CICode.generateStrings();
                CICode.closeIC();
            :} RBRACE;

SentenceBlock ::= Sentences |;

Sentences ::= Sentences Sentence | Sentence;

Expression ::= Expression:e1 PLUS Expression:e2 {:
                RESULT= plus(e1,e2);
            :} |
            Expression:e1 MINUS Expression:e2 {:
                RESULT= minus(e1,e2);
            :} |
            Expression:e1 MUL Expression:e2 {:
                RESULT= mul(e1,e2);
            :} |
            Expression:e1 DIV Expression:e2 {:
                RESULT= div(e1,e2);
            :} |
            INT:e {:
                RESULT= integer(e);
            :} |
            ID:id {:
                
            :} |
            LPAREN Expression:e RPAREN {:
                RESULT= e;
            :};
                

Condition ::= Condition:c1 OR Condition:c2 {:
                RESULT = or(c1,c2);
            :} |
            Condition:c1 AND Condition:c2 {:
                RESULT = and(c1,c2);
            :} |
            Expression:e1 EQUAL Expression:e2 {:
                RESULT = equal(e1,e2);
            :} |
            Expression:e1 UNEQUAL Expression:e2 {:
                RESULT = unequal(e1,e2);
            :} |
            Expression:e1 GREATER Expression:e2 {:
                RESULT = greater(e1,e2);
            :} |
            Expression:e1 LESS Expression:e2 {:
                RESULT = less(e1,e2);
            :} |
            LPAREN Condition:c RPAREN {:
                RESULT = c;
            :};

SentIf ::= IF LPAREN {:
                ifCounter++;
                pile.push(ifCounter);
            :} Condition:c {:
                condition(c, pile.peek());
            :} RPAREN LBRACE SentenceBlock RBRACE {:
                jumpLabel("FINIF", pile.peek());
                putLabel("ELSE", pile.peek());
            :}
            SentElse {:
                putLabel("FINIF", pile.peek());
                pile.pop();
            :};

SentElse ::= ELSE LBRACE SentenceBlock RBRACE |;

SentWhile ::= WHILE LPAREN {:
                loopCounter++;
                loopPile.push(loopCounter);
                putLabel("BUCLE",loopPile.peek());
            :} Condition:c {:
                condition_while(c,loopPile.peek());
            :} RPAREN LBRACE {:
                whileCounter++;
            :} SentenceBlock {:
                whileCounter--;
            :} RBRACE {:
                jumpLabel("BUCLE",loopPile.peek());
                putLabel("FINBUCLE",loopPile.peek());
                loopPile.pop();
            :};

SentAssign ::= ID:id {:
                if(SymTable.existSymbol(String.valueOf(id))){
                    
                } else {
                    int line = ((java_cup.runtime.Symbol)CUP$parser$stack.elementAt(CUP$parser$top)).left;
                    ERRORS.report(ERRORS.UNDECLARED_SYM,line);
                }
            :} ASSIGN Expression:e SEMICOLON {:
                assign(id,e);
            :};

SentPutw ::= PUTW LPAREN Expression:e {:
                printW(e);
            :} RPAREN SEMICOLON;

SentPuts ::= PUTS LPAREN STRING:s {:
                printCounter++;
                printS(s,printCounter);
            :} RPAREN SEMICOLON;

SentBreak ::= BREAK {:
            :} SEMICOLON;

Sentence ::=    SentIf |
                SentWhile |
                SentAssign |
                SentPutw |
                SentPuts |
                SentBreak;
